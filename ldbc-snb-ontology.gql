#string, long, double, boolean

# some generic resources
id "name" isa resource-type datatype string;
id "title" isa resource-type datatype string;
id "creation-date" isa resource-type datatype string;
id "browser-used" isa resource-type datatype string;
id "location-ip" isa resource-type datatype string;

# some basic entity-type with resource name
id "place" isa entity-type
    has-resource name;
id "organization" isa entity-type
    has-resource name;
id "language" isa entity-type
    has-resource name;
id "tag" isa entity-type
    has-resource name;
id "category" isa entity-type
    has-resource name;

# relation resides
id "location" isa role-type is-abstract;
place plays-role location;

id "subject-location" ako location;
id "located-subject" isa role-type;
id "resides" isa relation-type
    has-role subject-location
    has-role located-subject;
person plays-role located-subject;
message plays-role located-subject;
organization plays-role located-subject;

# place and relation sublocate
id "continent" ako place;
id "country" ako place;
id "city" ako place;

id "member-location" ako location;
id "container-location" ako location;
id "sublocate" isa relation-type
    has-role member-location
    has-role container-location;

# organization
id "university" ako organization;
id "company" ako organization;

# language
id "speaker" isa role-type;
id "language-spoken" isa role-type;
id "speaks" isa relation-type
    has-role speaker
    has-role language-spoken;
person plays-role speaker;
language plays-role language-spoken;

id "post-with-language" isa role-type;
id "language-written" isa role-type;
id "written-in" isa relation-type
    has-role post-with-language
    has-role language-written;
post plays-role post-with-language;
language plays-role language-written;

# person
id "firstname" isa resource-type datatype string;
id "lastname" isa resource-type datatype string;
id "gender" isa resource-type datatype string;
id "birthday" isa resource-type datatype string;
id "email" isa resource-type datatype string;
id "age" isa resource-type datatype long;

id "person" isa entity-type
    has-resource firstname
    has-resource lastname
    has-resource gender
    has-resource birthday
    has-resource email
    has-resource age
    has-resource creation-date
    has-resource browser-used
    has-resource location-ip;

id "acquaintance" isa role-type is-abstract;
id "acquaintance1" ako acquaintance;
id "acquaintance2" ako acquaintance;
id "knows" isa relation-type
    has-role acquaintance1
    has-role acquaintance2
    has-resource creation-date;
person plays-role acquaintance;

# relation attends
id "student" isa role-type;
id "enrolled-university" isa role-type;
id "class-year" isa resource-type datatype string;
id "attends" isa relation-type
    has-role student
    has-role enrolled-university
    has-resource class-year;
person plays-role student;
university plays-role enrolled-university;

# relation employment
id "employee" isa role-type;
id "employer" isa role-type;
id "employment-startdate" isa resource-type datatype string;
id "employment" isa relation-type
    has-role employee
    has-role employer
    has-resource employment-startdate;
person plays-role employee;
company plays-role employer;

# message
"content" isa resource-type datatype string;
"length" isa resource-type datatype string;

id "message" isa entity-type is-abstract
    has-resource creation-date
    has-resource browser-used
    has-resource location-ip
    has-resource content
    has-resource length;

# relation likes
id "liked" isa role-type;
id "liker" isa role-type;
id "likes" isa relation-type
    has-role liked
    has-role liker
    has-resource creation-date;
message plays-role liked;
person plays-role liker;

# relation writes
id "written" isa role-type;
id "writer" isa role-type;
id "writes" isa relation-type
    has-role written
    has-role writer;
message plays-role written;
person plays-role writer;

id "comment" ako message;

id "reply-owner" isa role-type;
id "reply-content" isa role-type;
id "reply" isa relation-type
    has-role reply-content
    has-role reply-owner;
comment plays-role reply-content;
message plays-role reply-owner;

"image-file" isa resource-type datatype string;

id "post" ako message has-resource image-file;

# forum
id "forum" isa entity-type
    has-resource title
    has-resource creation-date;

id "moderator" isa role-type;
id "moderated" isa role-type;
id "moderates" isa relation-type
    has-role moderator
    has-role moderated;
person plays-role moderator;
forum plays-role moderated;

id "forum-member" isa role-type;
id "membered-forum" isa role-type;
id "membership" isa relation-type
    has-role forum-member
    has-role membered-forum
    has-resource creation-date;
person plays-role forum-member;
forum plays-role membered-forum;

id "post-container" isa role-type;
id "contained-post" isa role-type;
id "containing" isa relation-type
    has-role post-container
    has-role contained-post;
post plays-role contained-post;
forum plays-role post-container;

# tag
id "subject-tag" isa role-type;
id "tagged-subject" isa role-type;
id "tagging" isa relation-type
    has-role tagged-subject
    has-role subject-tag;
tag plays-role subject-tag;
forum plays-role tagged-subject;
message plays-role tagged-subject;
person plays-role tagged-subject;

# tag class
id "group" isa role-type is-abstract;
id "subgroup" ako group;
id "supergroup" ako group;
id "subgrouping" isa relation-type
    has-role subgroup
    has-role supergroup;
category plays-role group;

id "grouped-tag" isa role-type;
id "tag-group" isa role-type;
id "grouping" isa relation-type
    has-role grouped-tag
    has-role tag-group;
tag plays-role grouped-tag;
category plays-role tag-group
